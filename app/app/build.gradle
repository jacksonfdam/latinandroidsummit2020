plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def gitVersion() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def versionMajor = 2
def versionMinor = 1
def versionRelease = 0
def versionPatch = gitVersion()
def version = (versionMajor * 1000000) + (versionMinor * 10000) + (versionRelease * 1000) + versionPatch


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.virtualconference.latinandroidsummit.app"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode version
        multiDexEnabled true
        manifestPlaceholders.screenOrientation = "portrait"
        versionName "${versionMajor}.${versionMinor}.${versionRelease}.${versionPatch}"
        buildConfigField 'String', 'version_code', '"v.' + versionName + '"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-defaults.txt'),
                    'proguard-rules-debug.pro'
            testProguardFile 'proguard-rules-test.pro'
            buildConfigField 'Boolean', 'debug_mode', 'true'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    flavorDimensions "app"
    productFlavors {
        latinAndroidSummit {
            dimension "app"
            applicationId "com.virtualconference.latinandroidsummit.app"
            manifestPlaceholders.hostName = "com.virtualconference.latinandroidsummit.app"
        }

        asianAndroidSummit {
            dimension "app"
            applicationId "com.virtualconference.asianandroidsummit.app"
            manifestPlaceholders.hostName = "com.virtualconference.asianandroidsummit.app"
        }

        europeAndroidSummit {
            dimension "app"
            applicationId "com.virtualconference.europeanandroidsummit.app"
            manifestPlaceholders.hostName = "com.virtualconference.europeanandroidsummit.app"
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
